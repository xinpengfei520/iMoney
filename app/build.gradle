plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

//apply plugin: 'com.growingio.android'
apply plugin: 'bugly'

//bugly {
//    // 注册时分配的 App ID
//    appId = 'c84e7e9ad7'
//    // 注册时分配的 App Key
//    appKey = '17ec4880-a08f-4bcd-9e69-46b96e639047'
//}

android {
    namespace 'com.xpf.p2p'
    useLibrary 'org.apache.http.legacy'
    compileSdk rootProject.ext.android.compileSdk
    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdk rootProject.ext.android.minSdk
        targetSdk rootProject.ext.android.targetSdk
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        flavorDimensions "versionCode"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        resValue("string", "growingio_project_id", "9addc2e4f77786ba")
        resValue("string", "growingio_url_scheme", "growing.2724aa529418df26")
        // 增加 gioenable 的配置
        resValue("string", "growingio_enable", project.gioenable)
        manifestPlaceholders = [
                JPUSH_APPKEY : "069229aa6eb2056acb0fa7e1",
                JPUSH_PKGNAME: rootProject.ext.android.applicationId,
                JPUSH_CHANNEL: "developer-default"
        ]
        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    signingConfigs {
        release {
            keyAlias "android"
            keyPassword "android"
            storeFile file("./android.jks")
            storePassword "android"
        }
        debug {
            keyAlias "android"
            keyPassword "android"
            storeFile file("./android.jks")
            storePassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            // API HOST(生产环境：pro 研发环境：dev 测试环境：test)
            buildConfigField 'String', 'HOST', getHOST('pro')
            // Log日志开关，false不显示log，true显示log
            buildConfigField "boolean", "ENABLE_DEBUG", "false"
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = getApkName()
                }
            }
        }

        debug {
            minifyEnabled false
            // API HOST(生产环境：pro 研发环境：dev 测试环境：test)
            buildConfigField 'String', 'HOST', getHOST('pro')
            // Log日志开关，false不显示log，true显示log
            buildConfigField "boolean", "ENABLE_DEBUG", "true"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += [
                "-Xjvm-default=all",
        ]
    }

    viewBinding { enable = true }

    lintOptions {
        abortOnError false
        disable 'ProtectedPermissions'
        disable 'GoogleAppIndexingWarning'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation("com.google.android.material:material:1.12.0")
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    testImplementation 'junit:junit:4.13.2'
    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation 'com.youth.banner:banner:1.4.10'
    implementation 'com.squareup.picasso:picasso:2.8'
    implementation 'com.github.iwgang:countdownview:2.1.6'
    implementation 'com.tencent.bugly:crashreport:latest.release'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.xsir:PgyerAndroidAppUpdate:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'me.jessyan:autosize:1.2.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.11'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.github.getActivity:XXPermissions:18.63'
    implementation 'cn.jiguang.sdk:jpush:3.3.6'
    implementation 'cn.jiguang.sdk:jcore:2.1.6'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.github.hackware1993:MagicIndicator:1.7.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.12'
    debugImplementation 'com.github.eleme.UETool:uetool:1.3.4'
    debugImplementation 'com.github.eleme.UETool:uetool-base:1.3.4'
    releaseImplementation 'com.github.eleme.UETool:uetool-no-op:1.3.4'
    // if you want to show more attrs about Fresco's DraweeView
    debugImplementation 'com.github.eleme.UETool:uetool-fresco:1.3.4'
    implementation 'com.growingio.android:vds-android-agent:autotrack-2.8.2@aar'
    implementation project(':okla-library')
}

def getApkFullPath() {
    return rootDir.getAbsolutePath() + "/app/build/outputs/apk/release/" + getApkName()
}

def getApkName() {
    return "iMoney_android_V${android.defaultConfig.versionName}_${android.defaultConfig.versionCode}_${releaseTime()}.apk"
}

static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

static def getUpdateDescription() {
    return '1.GrowingIO;\n2.fixed bugs;\n3.发布V1.4.5;'
}

tasks.register('uploadApk') {
    doLast {
        def command = "curl -F \"file=@${getApkFullPath()}\" -F \"uKey=${readProperties('pgyer.userKey')}\" -F \"_api_key=${readProperties('pgyer.apiKey')}\" -F \"buildUpdateDescription=${getUpdateDescription()}\" https://www.pgyer.com/apiv2/app/upload"
        executeCommand(command)
    }
}

uploadApk.dependsOn("assembleRelease")

tasks.register('installApk') {
    doLast {
        def command = "adb install -r ${getApkFullPath()}"
        executeCommand(command)
    }
}

tasks.register('launchApp') {
    doLast {
        def command = "adb shell am start ${android.defaultConfig.applicationId}/.MainActivity"
        executeCommand(command)
    }
}

// 获取API HOST
@SuppressWarnings("GroovyVariableNotAssigned")
def getHOST(String env) {
    def HOST
    def Properties props = new Properties()
    def propFile = file('src/main/env/' + env + '/config.properties')
    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile))
        if (props != null) {
            HOST = props['HOST']
        }
    }
    HOST
}

def executeCommand(command) {
    println "command:" + command
    try {
        exec {
            ExecSpec execSpec -> executable 'bash' args '-c', command
        }
        println "command execute success~"
    } catch (Exception e) {
        e.printStackTrace()
    }
}
/*
 * 打包命令：
 * ./gradlew assembleRelease
 * 打包上传 APK:
 * ./gradlew :app:uploadApk
 * 安装 APK:
 * ./gradlew :app:installApk
 * 启动 APK:
 * ./gradlew :app:launchApp
 */